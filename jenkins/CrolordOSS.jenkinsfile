#!groovy

@Library('CROLordSharedLibraryCode')  _

pipeline {
    // 定义使用的 Jenkins agent 类型
    agent { kubernetes { /* 配置省略 */ } }

    // 定义环境变量
    environment {
        GIT_BRANCH = 'main' // Git主分支的默认值
        MAJOR_VERSION = 'v1' // 主版本号
        MINOR_VERSION = '0'  // 次版本号
        PLATFORMS = 'linux/amd64,linux/arm64' // 构建目标平台
        MAJOR = "${params.MAJOR_VERSION ?: env.MAJOR_VERSION ?: '1'}" // 主版本号，允许通过参数覆盖
        MINOR = "${params.MINOR_VERSION ?: env.MINOR_VERSION ?: '0'}" // 次版本号，允许通过参数覆盖
        PATCH = "${env.BUILD_NUMBER}" // 构建号，用作修订版本号
        VERSION_TAG = "${MAJOR}.${MINOR}.${PATCH}" // 组合版本标签
        IMAGE_REGISTRY = "${params.IMAGE_REGISTRY}" // 镜像仓库地址
        IMAGE_NAMESPACE = "${params.IMAGE_NAMESPACE}" // 镜像命名空间
        IMAGE_ID = "${params.IMAGE_NAMESPACE}" // 镜像ID
        SONARQUBE_DOMAIN = "${params.SONARQUBE_DOMAINE}" // Sonarqube 域名配置
        DEPLOY_PATH = "${params.OSS_DEPLOY_PATH}"
        OSSENDPOINT = "${params.OSSENDPOINT}"
        OSSBUCKET = "${params.OSSBUCKET}"
        DEPLOY_ENVIRONMENT = "${params.DEPLOY_ENVIRONMENT}"  // 直接使用参数作为环境变量
    }

    // 触发条件
    triggers { githubPush() }

    // 参数定义
    parameters {
        persistentString(name: 'BRANCH', defaultValue: 'main', description: 'Initial default branch: main')
        persistentChoice(name: 'PLATFORMS', choices: ['linux/amd64', 'linux/amd64,linux/arm64'], description: 'Target platforms, initial value: linux/amd64,linux/arm64')
        persistentString(name: 'GIT_REPOSITORY', defaultValue: 'https://github.com/Roliyal/CROlordCodelibrary.git', description: 'Git repository URL, default: https://github.com/Roliyal/CROlordCodelibrary.git')
        persistentString(name: 'MAJOR_VERSION', defaultValue: '1', description: 'Major version number, default: 1')
        persistentString(name: 'MINOR_VERSION', defaultValue: '0', description: 'Minor version number, default: 0')
        persistentString(name: 'BUILD_DIRECTORY', defaultValue: 'Chapter2KubernetesApplicationBuild/Unit2CodeLibrary/FEBEseparation/vue-go-guess-number', description: 'Build directory path, default path: Chapter2KubernetesApplicationBuild/Unit2CodeLibrary/FEBEseparation/go-guess-number')
        persistentString(name: 'IMAGE_REGISTRY', defaultValue: 'crolord-registry-registry-vpc.cn-hongkong.cr.aliyuncs.com', description: 'Image registry address, default: crolord-registry-registry-vpc.cn-hongkong.cr.aliyuncs.com')
        persistentString(name: 'IMAGE_NAMESPACE', defaultValue: 'febe', description: 'Image namespace, default: febe')
        persistentString(name: 'SONARQUBE_DOMAINE', defaultValue: 'sonarqube.roliyal.com', description: 'SonarQube domain, default: sonarqube.roliyal.com')
        persistentString(name: 'OSS_DEPLOY_PATH', defaultValue: '', description: 'The OSS path where artifacts will be deployed')
        persistentString(name: 'OSSENDPOINT', defaultValue: 'oss-cn-hongkong.aliyuncs.com', description: 'The OSSEndpoin address default:oss-cn-hongkong.aliyuncs.com')
        persistentString(name: 'OSSBUCKET', defaultValue: 'febe', description: 'The OSS Bucket address default:febecrolord')
        choice(name: 'DEPLOY_ENVIRONMENT', choices: ['development', 'staging', 'production'], description: 'The deployment environment')
        booleanParam(name: 'REVERT_TO_PREVIOUS_VERSION', defaultValue: false, description: 'Select Yes to revert to previous version')
    }

    stages {
        stage('Revert to Previous Version') {
            agent { kubernetes { inheritFrom 'kanikoamd' } }
            when {
                expression {
                    return params.REVERT_TO_PREVIOUS_VERSION
                }
            }
            steps {
                container('kanikoamd') {
                    script {
                        // 创建环境变量的映射
                        def envVars = [:]
                        this.env.getEnvironment().each { key, value ->
                            envVars[key] = value
                        }
                        // 回滚到上一个版本
                        OSSDeployer.revertToPreviousVersion(this, envVars)
                    }
                }
            }
        }
    stage('Refresh CDN After Revert') {
        agent { kubernetes { inheritFrom 'kanikoamd' } }
            when {
            expression {
            return params.REVERT_TO_PREVIOUS_VERSION
                }
            }
        steps {
           // 指定在特定容器中执行
            container('kanikoamd') {
                script {
                    CDNRefresher.refresh(this)
                }
            }
        }
    }

        stage('Main Pipeline') {
            when {
                not {
                    expression {
                        return params.REVERT_TO_PREVIOUS_VERSION
                    }
                }
            }
            stages {
                stage('Version') {
                    steps {
                        script {
                            VersionManager.setVersion(this, env.MAJOR, env.MINOR)
                        }
                    }
                }

                stage('Checkout') {
                    steps {
                        script {
                            GitCheckout.checkout(this, params)
                        }
                    }
                }

                stage('Check Directory') {
                    steps {
                        script {
                            GitCheckout.stashCode(this)
                        }
                    }
                }

                stage('SonarQube analysis') {
                    agent { kubernetes { inheritFrom 'kanikoamd' } }
                    steps {
                        // 从之前的阶段恢复存储的源代码
                        unstash 'source-code'
                        // 指定在特定容器中执行
                        container('kanikoamd') {
                            // 设置SonarQube环境
                            withSonarQubeEnv('sonar') {
                                script {
                                    // 使用withCredentials从Jenkins凭据中获取SonarQube token
                                    withCredentials([string(credentialsId: 'sonar', variable: 'SONAR_TOKEN')]) {
                                        // 执行sonar-scanner命令
                                        sh """
                                        sonar-scanner \
                                          -Dsonar.projectKey=${JOB_NAME} \
                                          -Dsonar.projectName='${env.IMAGE_NAMESPACE}' \
                                          -Dsonar.projectVersion=${env.VERSION_TAG} \
                                          -Dsonar.sources=. \
                                          -Dsonar.exclusions='**/*_test.go,**/vendor/**' \
                                          -Dsonar.language=go \
                                          -Dsonar.host.url=http://${env.SONARQUBE_DOMAIN} \
                                          -Dsonar.login=${SONAR_TOKEN} \
                                          -Dsonar.projectBaseDir=${env.BUILD_DIRECTORY}
                                        """
                                    }
                                    // 使用script块处理HTTP请求和JSON解析
                                    withCredentials([string(credentialsId: 'sonar', variable: 'SONAR_TOKEN')]) {
                                        def authHeader = "Basic " + ("${SONAR_TOKEN}:".bytes.encodeBase64().toString())
                                        def response = httpRequest(
                                            url: "http://${env.SONARQUBE_DOMAIN}/api/qualitygates/project_status?projectKey=${JOB_NAME}",
                                            customHeaders: [[name: 'Authorization', value: authHeader]],
                                            consoleLogResponseBody: true,
                                            acceptType: 'APPLICATION_JSON',
                                            contentType: 'APPLICATION_JSON'
                                        )
                                        def json = readJSON text: response.content
                                        if (json.projectStatus.status != 'OK') {
                                            error "SonarQube quality gate failed: ${json.projectStatus.status}"
                                        } else {
                                            echo "Quality gate passed successfully."
                                        }
                                    }
                                }
                            }
                        }
                    }
                }

                stage('node oss push') {
                    agent { kubernetes { inheritFrom 'kanikoamd' } }
                    steps {
                        unstash 'source-code'
                        container('kanikoamd') {
                            script {
                                // 创建环境变量的映射
                                def envVars = [:]
                                this.env.getEnvironment().each { key, value ->
                                    envVars[key] = value
                                }
                                // 执行文件系统扫描
                                TrivyScanner.scanFileSystem(this, envVars.BUILD_DIRECTORY)
                                // 部署到OSS
                                OSSDeployer.deploy(this, params, envVars)
                            }
                        }
                    }
                }

                stage('Refresh CDN') {
                    agent { kubernetes { inheritFrom 'kanikoamd' } }
                    steps {
                        container('kanikoamd') {
                            script {
                                CDNRefresher.refresh(this)
                            }
                        }
                    }
                }
            }
        }
    }
}